plugins {
    id "com.gradle.plugin-publish" version "0.15.0"
    id "org.jetbrains.kotlin.jvm"
    id "java-gradle-plugin"
    id "maven-publish"
}

dependencies {
    compileOnly "com.android.tools.build:gradle:$versions.androidGradlePlugin"
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
    testImplementation gradleTestKit()
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.assertj:assertj-core:3.19.0"
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.withType(PluginUnderTestMetadata).configureEach {
    // Plugin doesn't declare any `implementation` dependency. We'll use its compile time dependencies to as a test environment
    pluginClasspath.from(configurations.compileOnly)
}

def kotlinVersion = plugins.getPlugin("kotlin").kotlinPluginVersion
configurations.configureEach {
    resolutionStrategy.eachDependency {
        if (requested.group == 'org.jetbrains.kotlin') {
            useVersion kotlinVersion
        }
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

gradlePlugin {
    plugins {
        disabler {
            id = 'pl.droidsonroids.animation-disabler'
            implementationClass = 'pl.droidsonroids.gradle.animation.AnimationDisablerPlugin'
        }
    }
}

group = GROUP
version = VERSION_NAME

pluginBundle {
    website = POM_URL
    vcsUrl = POM_SCM_URL
    description = POM_DESCRIPTION
    tags = ['android', 'ci', 'espresso', 'test', 'UI', 'animation']

    plugins {
        disabler {
            displayName = POM_NAME
            description = POM_DESCRIPTION
        }
    }
}
